---
layout: post
title:  "IoT-Ep5 | HTTP POST gửi dữ liệu lên Thingspeak"
date:   2022-03-22 00:00:00 +0300
category: Tech
image: /tech-blog/assets/PostRequest.png
tags: [IoT, Thingspeak, HTTP]
---
Trong bài viết này, chúng ta sẽ đi tìm hiểu cách sử dụng phương thức POST (HTTP POST request) để cập nhật dữ liệu cảm biến trên Thingspeak.

# Mục lục
1. [Giới thiệu](#Section1)
2. [Phương thức POST](#Section2)
3. [Kết nối và lập trình](#Section3)
- [3.1. Kết nối](#Subsection3.1)
- [3.2 Sử dụng HTTP POST request](#Subsection3.2)
4. [Kết luận](#Section4)
5. [Tài liệu tham khảo](#Section5)

### 1. Giới thiệu <a name="Section1"></a>
Các phương thức **HTTP requests** được sử dụng tuỳ theo những yêu cầu riêng. Nền tảng Thingspeak ngoài hỗ trợ cập nhật dữ liệu bằng phương thức **HTTP GET**, cũng hỗ trợ phương thức **HTTP POST** để gửi yêu cầu cập nhật dữ liệu trên **Channel**.
Khi sử dụng phương thức **GET**, tất cả dữ liệu biểu mẫu được mã hoá thành URL, và được nối vào URL dưới dạng tham số chuỗi truy vấn. Còn khi sử dụng **POST**, dữ liệu biểu mẫu xuất hiện trong nội dung yêu cầu của **HTTP POST request**.

Trong bài này, chúng ta sẽ tìm hiểu về phương thức **POST** mà Thingspeak hỗ trợ để cập nhật dữ liệu và xem cách lập trình trên NodeMCU để gửi các yêu cầu **POST** đính kèm dữ liệu nhiệt độ và độ ẩm từ cảm biển DHT22.

### 2. Phương thức POST <a name="Section2"></a>
Trong phương thức **POST**, dữ liệu gửi đi nằm trong phần **Body** của **request**. Theo như [tài liệu Thingspeak](https://www.mathworks.com/help/thingspeak/writedata.html) thì việc gửi dữ liệu bằng cách sử dụng phương thức **POST** có thể biểu diễn ở 2 cách sau. Trong phần **Body** của yêu cầu **Request** thì dữ liệu của chúng ta có thể biểu diễn ở dạng **URL encoded** hoặc ở dạng **JSON**.

|Request        |POST /update.json HTTP/1.1                                 |
|Body           |{"api_key": "RBDRQF9W9SDPJA6T","field1":10,"field2":30}    |
|Kiểu dữ liệu   |Content-Type: application/json                             |

<p align="center">
  <img alt="Edit bieu do" src="/tech-blog/assets/PostRequest.png" style="zoom:50%">
  <br>
    <em>Hình 1. Gửi application/json theo phương thức **POST**</em>
</p>

hoặc

|Request        |POST /update HTTP/1.1                                |
|Body           |api_key=RBDRQF9W9SDPJA6&field1=10&field2=30          |
|Kiểu dữ liệu   |Content-Type: application/x-www-form-urlencoded      |

<p align="center">
  <img alt="Edit bieu do" src="/tech-blog/assets/PostRequest2.png" style="zoom:50%">
  <br>
    <em>Hình 2. Gửi application/x-www-form-urlencoded theo phương thức **POST**</em>
</p>


### 3. Kết nối và lập trình <a name="Section3"></a>
#### 3.1. Kết nối <a name="Subsection3.1"></a>
Trong bài này, chúng ta tiếp tục làm việc với NodeMCU và DHT22. Cảm biến có thể được kết nối tới mô-đun NodeMCU theo sơ đồ Hình 3.
<p align="center">
  <img alt="Library Manager" src="/tech-blog/assets/NodeMCU_DHT.png" style="zoom:50%">
  <br>
    <em>Hình 3. Sơ đồ kết nối NodeMCU với DHT22</em>
</p>

#### 3.2. Sử dụng **HTTP POST request** <a name="Subsection3.2"></a>
Có 2 cách lập trình trên Arduino IDE cho NodeMCU có thể gửi **HTTP POST request** tới máy chủ (Server) từ xa:
- Cách 1: chúng ta chỉ sử dụng 1 thư viện **ESP8266WiFi.h**.
- Cách 2: chúng ta có thể sử dụng kết hợp 2 thư viện **ESP8266WiFi.h** và **ESP8266HTTPClient.h**.

**a. Sử dụng ESP8266WiFi.h**: các bạn có thể tham khảo **Sketch** sau đây.
{% highlight C %}
#include <ESP8266WiFi.h>
#include "DHT.h"

// You need to edit with your network credentials.
#define WIFI_NAME "YOUR_WIFI_NAME"
#define WIFI_PASSWORD "YOUR_WIFI_PASSWORD"

// Initialize the client library
WiFiClient client;

// You need to edit with your ThingSpeak information.
#define THING_SPEAK_ADDRESS "api.thingspeak.com"
String writeAPIKey="YOUR_WRITE_API_KEY";

// DHT information
#define DHTPIN D2
#define DHTTYPE DHT22
DHT dht(DHTPIN, DHTTYPE);

unsigned long lastTime = 0;
unsigned long delayTime = 20000; // set a period of sending data.

void setup()
{
  Serial.begin(115200);
  Serial.println();

  WiFi.begin(WIFI_NAME, WIFI_PASSWORD);

  Serial.print("Connecting to WiFi");
  while (WiFi.status() != WL_CONNECTED)
  {
    delay(500);
    Serial.print(".");
  }
  Serial.println();

  Serial.print("Connected, IP address: ");
  Serial.println(WiFi.localIP());

  // Begin DHT Sensor
  dht.begin();
  
  delay(2000);
}

void loop() {
  if ((millis() - lastTime) > delayTime) {
    HTTPPost();
    lastTime = millis();
  }
}

void HTTPPost() {
  if (client.connect(THING_SPEAK_ADDRESS, 80)) {
    Serial.println("Server connected");

    // Read DHT sensor
    float h = dht.readHumidity();    // Read humidity as %
    float t = dht.readTemperature(); // Read temperature as Celsius (the default)
    // Check if any reads failed and exit early (to try again).
    if (isnan(h) || isnan(t)) {
      Serial.println(F("Failed to read from DHT sensor!"));
      return;
    }

    Serial.println("Sending POST request:");
    // String postData= "api_key=" + writeAPIKey + "&field1=" + String(t) + "&field2=" + String(h);
    String postData = "{\"api_key\":\"" + writeAPIKey + "\",\"field1\":\"" + String(t) + "\",\"field2\":\"" + String(h) + "\"}";
    // client.println("POST /update HTTP/1.1");
    client.println("POST /update.json HTTP/1.1");
    client.println( "Host: api.thingspeak.com" );
    client.println( "Connection: close" );
    // client.println( "Content-Type: application/x-www-form-urlencoded" );
    client.println( "Content-Type: application/json" );
    client.println( "Content-Length: " + String( postData.length() ) );
    client.println();
    client.println( postData );
    Serial.println( postData );

    unsigned long timeout = millis();
    while (client.available() == 0) {
      if (millis() - timeout > 5000){
        Serial.println(">>> Client Timeout !");
        client.stop(); return;
        }
    }
    
    // Read all the lines of the reply from server and print them to Serial
    while (client.available()){
      String line = client.readStringUntil('\r'); Serial.print(line);
    }
    
    Serial.println("\nClosing connection\n");
  }
}
{% endhighlight %}

Thay đổi thông tin kết nối WiFi dành cho mô-đun:
{% highlight C %}
// You need to edit with your network credentials.
#define WIFI_NAME "YOUR_WIFI_NAME"
#define WIFI_PASSWORD "YOUR_WIFI_PASSWORD"
{% endhighlight %}

Tiếp theo, để cập nhật dữ liệu lên Thingspeak, chúng ta cũng cần tới **Write API Key** trên kênh của mình.
{% highlight C %}
// You need to edit with your ThingSpeak information.
String writeAPIKey="YOUR_WRITE_API_KEY";
{% endhighlight %}

Câu lệnh **HTTP POST request** được thực thi tại đây để yêu cầu cập nhật 2 giá trị cho **field1** và **field2**. Trong đoạn code này, chúng ta sử dụng kiểu dữ liệu **JSON** cho thông tin gửi đi. Ngoài ra, các bạn có thể xem các dòng code được **Commnent** nếu bạn muốn gửi dữ liệu **urlencoded**.
{% highlight C %}
Serial.println("Sending POST request:");
// String postData= "api_key=" + writeAPIKey + "&field1=" + String(t) + "&field2=" + String(h);
String postData = "{\"api_key\":\"" + writeAPIKey + "\",\"field1\":\"" + String(t) + "\",\"field2\":\"" + String(h) + "\"}";
// client.println("POST /update HTTP/1.1");
client.println("POST /update.json HTTP/1.1");
client.println( "Host: api.thingspeak.com" );
client.println( "Connection: close" );
// client.println( "Content-Type: application/x-www-form-urlencoded" );
client.println( "Content-Type: application/json" );
client.println( "Content-Length: " + String( postData.length() ) );
client.println();
client.println( postData );
Serial.println( postData );
{% endhighlight %}

Sau khi **Upload** chương trình lên mô-đun, chúng ta có thể theo dõi **Response Code** trả về qua **Serial Monitor**. Code trả về **200 OK** có nghĩa là yêu cầu cập nhật dữ liệu thành công (Hình 4).
<p align="center">
  <img alt="Serial Monitor" src="/tech-blog/assets/SerialMonitor3.png" style="zoom:50%">
  <br>
    <em>Hình 4. Code trả về qua Serial Montior</em>
</p>

**b. Sử dụng ESP8266WiFi.h** và **ESP8266HTTPClient.h**. Chúng ta có thể sử dụng **Sketch** sau:
{% highlight C %}
#include <ESP8266WiFi.h>
#include <ESP8266HTTPClient.h>
#include "DHT.h"

// You need to edit with your network credentials.
#define WIFI_NAME "YOUR_WIFI_NAME"
#define WIFI_PASSWORD "YOUR_WIFI_PASSWORD"

// You need to edit with your ThingSpeak information.
String writeAPIKey = "YOUR_WRITE_API_KEY";

// DHT information
#define DHTPIN D2
#define DHTTYPE DHT22
DHT dht(DHTPIN, DHTTYPE);

unsigned long lastTime = 0;
unsigned long delayTime = 20000; // set a period of sending data.

void setup()
{
  Serial.begin(115200);
  Serial.println();

  WiFi.begin(WIFI_NAME, WIFI_PASSWORD);

  Serial.print("Connecting to WiFi");
  while (WiFi.status() != WL_CONNECTED)
  {
    delay(500);
    Serial.print(".");
  }
  Serial.println();

  Serial.print("Connected, IP address: ");
  Serial.println(WiFi.localIP());

  // Begin DHT Sensor
  dht.begin();
  
  delay(2000);
}

void loop() {
    //Send an HTTP POST request every 20 seconds
    if ((millis() - lastTime) > delayTime) {
        httpPostRequest();
        lastTime = millis();
    }
}

void httpPostRequest() {
    WiFiClient client;
    HTTPClient http;
    
    // Read DHT sensor
    float h = dht.readHumidity();    // Read humidity as %
    float t = dht.readTemperature(); // Read temperature as Celsius (the default)
    // Check if any reads failed and exit early (to try again).
    if (isnan(h) || isnan(t)) {
        Serial.println(F("Failed to read from DHT sensor!"));
        return;
    }
    
    http.begin(client, "http://api.thingspeak.com/update.json");
    /*
    // Specify content-type header
    http.addHeader("Content-Type", "application/x-www-form-urlencoded");
    // Data to send with HTTP POST
    String httpRequestData = "api_key=" + writeAPIKey + "&field1=" + String(t) + "&field2=" + String(h);
    // Send HTTP POST request
    int httpResponseCode = http.POST(httpRequestData);
    */
    
    // If you need an HTTP request with a content type: application/json, use the following:
    http.addHeader("Content-Type", "application/json");
    // JSON data to send with HTTP POST
    String httpRequestData = "{\"api_key\":\"" + writeAPIKey + "\",\"field1\":\"" + String(t) + "\",\"field2\":\"" + String(h) + "\"}";
    // Send HTTP POST request
    Serial.println("Sending HTTP POST request");
    int httpResponseCode = http.POST(httpRequestData);
     
    String payload = "{}"; 
  
    if (httpResponseCode>0) {
        Serial.print("HTTP Response code: ");
        Serial.println(httpResponseCode);
        payload = http.getString();
        Serial.println(payload);
    }
    else {
        Serial.print("Error code: ");
        Serial.println(httpResponseCode);
    }
    // Free resources
    http.end();
}
{% endhighlight %}

Trong **Sketch** này, thư viện **ESP8266HTTPClient.h** cho phép chúng ta khởi tạo một đối tượng **HTTPClient** và các phương thức **HTTP requests** được hỗ trợ ở đây. Đối tượng **http** gọi phương thức **POST()** để gửi **POST URL Request**.
{% highlight C %}
WiFiClient client;
HTTPClient http;
http.begin(client, "http://api.thingspeak.com/update.json");
/*
// Specify content-type header
http.addHeader("Content-Type", "application/x-www-form-urlencoded");
// Data to send with HTTP POST
String httpRequestData = "api_key=" + writeAPIKey + "&field1=" + String(t) + "&field2=" + String(h);
// Send HTTP POST request
int httpResponseCode = http.POST(httpRequestData);
*/
    
// If you need an HTTP request with a content type: application/json, use the following:
http.addHeader("Content-Type", "application/json");
// JSON data to send with HTTP POST
String httpRequestData = "{\"api_key\":\"" + writeAPIKey + "\",\"field1\":\"" + String(t) + "\",\"field2\":\"" + String(h) + "\"}";
// Send HTTP POST request
Serial.println("Sending HTTP POST request");
int httpResponseCode = http.POST(httpRequestData);
{% endhighlight %}

Sau khi gửi **POST** request, máy chủ gửi lại **HTTP code response** để xác nhận yêu cầu đã gửi thành công. Ngoài ra, Thingspeak cũng gửi lại thông tin của **Channel** dưới dạng **JSON**.
{% highlight C %}
String payload = "{}"; 
  
if (httpResponseCode>0) {
    Serial.print("HTTP Response code: ");
    Serial.println(httpResponseCode);
    payload = http.getString();
    Serial.println(payload);
}
else {
    Serial.print("Error code: ");
    Serial.println(httpResponseCode);
}
{% endhighlight %}
Tương như trong trường hợp trên, sau khi **Upload** chương trình lên mô-đun, chúng ta có thể theo dõi **Response Code** trả về qua **Serial Monitor**. Code trả về **200** có nghĩa là yêu cầu cập nhật dữ liệu thành công (Hình 5).
<p align="center">
  <img alt="Serial Monitor" src="/tech-blog/assets/SerialMonitor4.png" style="zoom:50%">
  <br>
    <em>Hình 5. Code trả về qua Serial Montior</em>
</p>

### 4. Kết luận <a name="Section4"></a>
Như vậy, chúng ta có thể sử dụng **HTTP POST request** để cập nhật dữ liệu trên Thingspeak. Trong 2 cách lập trình cho NodeMCU, chúng ta đều nhận được các kết quả như nhau, dữ liệu được cập nhật thành công trên Thingspeak và trả về code **200** (Hình 6). 
<p align="center">
  <img alt="Bieu Do" src="/tech-blog/assets/Thingspeak_BieuDo2.png" style="zoom:50%">
  <br>
    <em>Hình 6. Đồ thị biểu diễn dữ liệu trên Thingspeak</em>
</p>

### 5. Tài liệu tham khảo <a name="Section5"></a>
- Code mẫu: [https://github.com/itcomnetworks/IoT-Series](https://github.com/itcomnetworks/IoT-Series){:target="_blank"}
- [Write Data - Update channel data with HTTP GET or POST](https://www.mathworks.com/help/thingspeak/writedata.html){:target="_blank"}
- [HTTP Request Methods](https://www.w3schools.com/tags/ref_httpmethods.asp){:target="_blank"}
- DHT Sensor Library: [https://github.com/adafruit/DHT-sensor-library](https://github.com/adafruit/DHT-sensor-library){:target="_blank"}